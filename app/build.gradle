apply plugin: 'com.android.application'
apply plugin: 'com.antfortune.freeline'
//不要加下面这句，会出错：
//java.lang.NoSuchMethodError: com.github.javaparser.ast.CompilationUnit.getTypes()Lcom/github/javaparser/ast/NodeList;
//apply plugin: 'com.jakewharton.butterknife'

//apply from: 'tinker-support.gradle'
//因为使用freeline时会报tinker错误，所以执行assembleRel命令时才引入'tinker-support.gradle'
task myTask {
    List<TaskExecutionRequest> taskRequests = gradle.startParameter.getTaskRequests()
    if (taskRequests.size() > 0) {
        String firstTask = taskRequests.get(0).toString()
        if (firstTask.contains("assembleRel")) {
            apply from: 'tinker-support.gradle'
        }
    }
}

def outputPathName = "${rootDir}/apk"

def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("GMT+08"))
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.example.sqhan.artwork"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                File outputDirectory = new File(outputPathName)
                def fileName
                if (variant.buildType.name == "release") {
                    fileName = "ArtWork_v${defaultConfig.versionName}_${buildTime()}_release.apk"
                    output.outputFile = new File(outputDirectory, fileName)
                } else if (variant.buildType.name == "debug") {
                    fileName = "ArtWork_v${defaultConfig.versionName}_${buildTime()}_debug.apk"
                    output.outputFile = new File(outputDirectory, fileName)
                }
            }
            //只有打release包时才拷贝release版的R文件
            if ("${variant.baseName}" == "release") {
                copy {
                    def fileNamePrefix = "vbk_v${defaultConfig.versionName}-${variant.baseName}-${buildTime()}"
                    from "${buildDir}/intermediates/symbols/${variant.dirName}/R.txt"
                    into outputPathName
                    rename { String fileName1 ->
                        fileName1.replace("R.txt", "${fileNamePrefix}-R.txt")
                    }
                }
            }
        }
    }

    //不使用这个，在mcd中VBK应用会失败
    /*applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (variant.buildType.name == "release") {
                output.outputFile = new File(file.parent, file.name.replace(".apk", "ArtWork_v${defaultConfig.versionName}_${buildTime()}.apk"))
            } else if (variant.buildType.name == "debug") {
                output.outputFile = new File(file.parent, file.name.replace(".apk", "ArtWork_v${defaultConfig.versionName}_${buildTime()}.apk"))
            }
        }
    }*/

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'junit:junit:4.12'
    compile 'org.greenrobot:eventbus:3.1.1'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile "com.android.support:multidex:1.0.1" // 多dex配置

    //tinker热修复
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    // 指定tinker依赖版本（注：应用升级1.3.5版本起，不再内置tinker）
    compile 'com.tencent.tinker:tinker-android-lib:1.9.6'
    compile 'com.tencent.bugly:nativecrashreport:latest.release'
    //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    compile 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'

    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'


}

task cleanApk() {
    File file1 = new File(outputPathName)
    if (file1.exists()) {
        file1.deleteDir()
    }
}
clean.dependsOn cleanApk
